
1. 센스리더 오토메이션 소개

오토메이션이란 한 응용프로그램이 다른 응용프로그램에서 사용할 수 있도록 자신의 기능을 외부에 제공하는 표준화된 COM 기술의 하나이다.
오토메이션을 제공하는 대표적 프로그램으로 Microsoft Office를 들 수 있다.
센스리더 오토메이션 기능은 이런 오토메이션 기술을 사용해 센스리더의 내부 객체를 외부에 제공하는 기능을 말한다.
따라서 COM을 사용할 수 있는 곳이라면 어디서든 센스리더 객체에 접근할 수 있다.
현재 센스리더 오토메이션을 통해 음성 제어 기능이 제공된다.

2. 센스리더 객체 접근하기

센스리더 오토메이션 객체모델의 최상위 객체는 센스리더의 Application객체이며 이 객체를 얻는 작업이 센스리더 객체에 접근하기 위해 제일 먼저 해야 할 작업이다.
Application객체가 얻어지면 이 객체가 제공하는 메소드인 Speak, StopSpeaking 등을 사용할 수 있다.
Application객체는 ProgID "SenseReader.Application"을 사용하여 얻을 수 있다.

=== Sample 1: 센스리더 객체를 통해 음성 출력하는 예제 ===
<html>
<head>
<title>Sample1</title>
<script language="VBScript">
sub speak()
' 센스리더 Application 객체를 얻는다.
set app = createobject("sensereader.application")
' 편집창에 입력된 내용을 센스리더 기본 음성으로 출력한다.
app.speak testform.speaktext.value
end sub
</script>
</head>
<body>
<form name="testform">
<textarea name="speaktext" cols="100" rows="10"></textarea>
<p>
<input type="button" value="재생" onclick="speak">
</form>
</body>
</html>

3. 독립된 음성 콘트롤

Application.Speak 및 Application.StopSpeaking은 센스리더의 기본 음성을 사용한다.
따라서 현재 센스리더 음성의 속도, 높이, 크기 등의 음성 설정을 그데로 사용하며 음성출력 도중 키 입력이 있으면 음성은 자동으로 중지된다.
만일 센스리더 기능과는 별개의 음성 제어가 필요한 경우 다음과 같이 별도의 SpeechControler객체를 얻어 사용한다.

1) ProgID "SenseReader.Application"을 사용해 Application객체를 얻는다.
2) Application.SpeechControlers를 통해 SpeechControlers객체를 얻는다.
3) SpeechControlers.Add에 새로운 콘트롤러의 이름을 전달하여 SpeechControler객체를 얻는다.
4) SpeechControler객체를 초기화한다.
5) SpeechControler.NewSpeakInfo를 사용하여 음성 출력에 사용할 SpeakInfo객체를 얻는다.
6) SpeakInfo의 속성 Speed, Pitch, Volume 등의 값을 원하는 값으로 설정한다.
7) SpeechControler.Speak에 출력할 문자열과 SpeakInfo객체를 전달하여 음성을 출력한다.

=== Sample 2: 독립 음성 콘트롤러를 통해 음성 출력하는 예제 ===
<html>
<head>
<title>Sample2</title>
<script language="VBScript">
' 음성 속성(속도, 높이 크기)의 최소 최대값을 위한 상수
const minspeakvalue = 0
const maxspeakvalue = 10
' 센스리더 Application객체를 얻는다.
set app = createobject("sensereader.application")
' SpeechControlers객체를 얻는다.
set ctrls = app.speechcontrolers
' "test"란 이름으로 새로운 SpeechControler객체를 얻는다.
set ctrl = ctrls.add("test")
' SpeechControler객체를 초기화한다.
ctrl.init ""
' 음성 출력에 사용할 SpeakInfo객체를 얻는다.
set speakinfo = ctrl.newspeakinfo
' 스페이스와 리턴문자 읽기를 해제한다.
speakinfo.flags = speakinfo.flags-(32+128)

' 편집창에 입력된 텍스트를 음성출력하는 함수
sub speak()
stopandspeak(testform.speaktext.value)
end sub

' 전달된 문자열을 음성출력하는 함수. 음성이 출력중이면 먼저 중지시킨후 출력한다.
sub stopandspeak(text)
stopspeaking
ctrl.speak text, speakinfo
end sub

' 음성을 중지하는 함수
sub stopspeaking()
ctrl.stop
end sub

' 속도를 증가시키는 함수
sub speedup()
if speakinfo.speed < maxspeakvalue then
speakinfo.speed = speakinfo.speed+1
end if
str = "속도 " & speakinfo.speed
stopandspeak(str)
end sub

' 속도를 감소시키는 함수
sub speeddown()
if speakinfo.speed > minspeakvalue then
speakinfo.speed = speakinfo.speed-1
end if
str = "속도 " & speakinfo.speed
stopandspeak(str)
end sub

' 높이를 증가시키는 함수.
sub pitchup()
if speakinfo.pitch < maxspeakvalue then
speakinfo.pitch = speakinfo.pitch+1
end if
str = "높이 " & speakinfo.pitch
stopandspeak(str)
end sub

' 높이를 감소시키는 함수.
sub pitchdown()
if speakinfo.pitch > minspeakvalue then
speakinfo.pitch = speakinfo.pitch-1
end if
str = "높이 " & speakinfo.pitch
stopandspeak(str)
end sub

' 크기를 증가시키는 함수.
sub volumeup()
if speakinfo.volume < maxspeakvalue then
speakinfo.volume = speakinfo.volume+1
end if
str = "크기 " & speakinfo.volume
stopandspeak(str)
end sub

' 크기를 감소시키는 함수.
sub volumedown()
if speakinfo.volume > minspeakvalue then
speakinfo.volume = speakinfo.volume-1
end if
str = "크기 " & speakinfo.volume
stopandspeak(str)
end sub
</script>
</head>
<body>
<form name="testform">
<textarea name="speaktext" cols="100" rows="10"></textarea>
<p>
<input type="button" value="재생" onclick="speak">&nbsp;
<input type="button" value="중지" onclick="stopspeaking">
<br>
<input type="button" value="속도 증가" onclick="speedup">&nbsp;
<input type="button" value="속도 감소" onclick="speeddown">
<br>
<input type="button" value="높이 증가" onclick="pitchup">&nbsp;
<input type="button" value="높이 감소" onclick="pitchdown">
<br>
<input type="button" value="크기 증가" onclick="volumeup">&nbsp;
<input type="button" value="크기 감소" onclick="volumedown">
</form>
</body>
</html>
